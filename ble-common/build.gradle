apply plugin: 'com.android.library'
apply plugin: 'android-maven'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    PUBLISH_GROUP_ID = 'ai.longev.android'
    PUBLISH_ARTIFACT_ID = 'ble-common'
    PUBLISH_VERSION = '2.1.3'

    bintrayRepo = 'android'
    bintrayName = 'ble-common-library'

    publishedGroupId = PUBLISH_GROUP_ID
    artifact = PUBLISH_ARTIFACT_ID
    libraryVersion = PUBLISH_VERSION
    libraryName = 'BLE Common Library'
    libraryDescription = 'Bluetooth Low Energy library for Android'

    issuesUrl = 'https://github.com/ndrd/Android-BLE-Common-Library/issues'
    siteUrl = 'https://github.com/ndrd/Android-BLE-Common-Library'
    gitUrl = 'https://github.com/ndrd/Android-BLE-Common-Library.git'

    developerId = 'ndrd'
    developerName = 'Jonathan Andrade'
    developerEmail = 'j@longev.ai'

    licenseName = 'The BSD 3-Clause License'
    licenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'
    allLicenses = ["BSD 3-Clause"]
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 28
        versionName "2.1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Import the BLE Library
    api 'ai.longev.android:ble:2.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'
}


// The following script creates a POM file required to publish on Maven Central
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'

// This script creates sources and javadocs. Both are required to publish a library on jcenter and MC.
apply from: 'https://raw.githubusercontent.com/ArthurHub/release-android-library/master/android-release-aar.gradle'

// The following link publishes the library to jcenter. It does not handle the userOrg, so it has been copied and modified below.
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

// Copied from https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle
version = libraryVersion

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = properties.getProperty("bintray.userOrg")
        desc = libraryDescription
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
